apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'kotlin-allopen'

allOpen {
    annotation 'cat.xlagunas.data.OpenClass'
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "cat.xlagunas.viv"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode rootProject.gitCommitCount
        versionName rootProject.gitTag
        testInstrumentationRunner "cat.xlagunas.viv.commons.di.MockTestRunner"
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('../buildsystem/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file('../buildsystem/production.keystore')
            storePassword System.getenv("PROD_KEYSTORE_PASSWORD")
            keyAlias System.getenv("PROD_KEY_ALIAS")
            keyPassword System.getenv("PROD_KEY_PASSWORD")
        }
    }

    flavorDimensions "version"

    productFlavors {
        production {
        }

        development {
            versionNameSuffix '.local'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = ["appName": '@string/app_name']
        }

        debug {
            minifyEnabled isMinifyEnabledForBuild()
            useProguard isMinifyEnabledForBuild()
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.dev'
            versionNameSuffix '.dev'
            manifestPlaceholders = ["appName": '@string/app_name_dev']
        }

    }

    dataBinding {
        enabled = true
    }

    testOptions {
        animationsDisabled = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
//    dynamicFeatures = [":conference"]

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':data')
    implementation project(':domain')
    kapt 'com.google.dagger:dagger-compiler:2.19'
    implementation 'com.google.dagger:dagger:2.19'
    kapt 'com.jakewharton:butterknife-compiler:9.0.0-rc1'
    implementation 'com.jakewharton:butterknife:9.0.0-rc1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.media:media:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.1'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.0.0'
    implementation 'android.arch.navigation:navigation-fragment-ktx:1.0.0-alpha07'
    implementation 'android.arch.navigation:navigation-ui-ktx:1.0.0-alpha07'
    implementation 'androidx.room:room-runtime:2.1.0-alpha02'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'
    implementation 'com.google.firebase:firebase-core:16.0.5'
    implementation 'com.google.firebase:firebase-analytics:16.0.5'
    implementation 'com.google.firebase:firebase-perf:16.2.0'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.3'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.github.tomakehurst:wiremock:2.8.0') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'asm', module: 'asm'
        exclude group: 'org.json', module: 'json'
    }
    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.19'
    kaptTest 'com.google.dagger:dagger-compiler:2.19'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0'
    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestImplementation 'android.arch.navigation:navigation-testing-ktx:1.0.0-alpha06'
    androidTestImplementation 'androidx.arch.core:core-testing:2.0.0'
    androidTestImplementation 'org.mockito:mockito-core:2.19.0', { exclude group: 'net.bytebuddy' }
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.19.0'
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation project(':core')

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

static def isMinifyEnabledForBuild() {
    String minifyStatus = System.getenv("MINIFY_ENABLED")
    if (minifyStatus != null) {
        return minifyStatus.toBoolean()
    }
    return false
}

project.tasks.whenTaskAdded {
    if (it.name == "testReleaseUnitTest") {
        it.enabled = false
    }
}

apply plugin: 'com.google.gms.google-services'
