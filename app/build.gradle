apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-allopen'

allOpen {
    annotation 'cat.xlagunas.core.OpenClass'
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "cat.xlagunas.viv"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode rootProject.gitCommitCount
        versionName rootProject.gitTag
        testInstrumentationRunner "cat.xlagunas.viv.commons.MockTestRunner"
    }

    signingConfigs {
        debug {
            storeFile file('../buildsystem/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file('../buildsystem/production.keystore')
            storePassword System.getenv("PROD_KEYSTORE_PASSWORD")
            keyAlias System.getenv("PROD_KEY_ALIAS")
            keyPassword System.getenv("PROD_KEY_PASSWORD")
        }
    }

    flavorDimensions "version"

    productFlavors {
        production {
        }

        development {
            versionNameSuffix '.local'
        }
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = ["appName": '@string/app_name']
        }

        debug {
            minifyEnabled isMinifyEnabledForBuild()
            useProguard isMinifyEnabledForBuild()
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.dev'
            versionNameSuffix '.dev'
            manifestPlaceholders = ["appName": '@string/app_name_dev']
        }
    }

    testOptions {
        animationsDisabled = true
    }


    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    dynamicFeatures = [":conference"]
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':push')
    implementation project(':call')
    implementation project(':contact')
    implementation project(':user')
    implementation project(':core')

    kapt 'com.google.dagger:dagger-compiler:2.21'
    implementation 'com.google.dagger:dagger:2.21'

    implementation 'com.google.android.material:material:1.1.0'

    implementation 'androidx.lifecycle:lifecycle-runtime:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    implementation 'androidx.room:room-runtime:2.1.0-beta01'
    implementation "androidx.fragment:fragment-ktx:1.2.4"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.google.firebase:firebase-analytics:16.5.0'
    implementation 'com.google.firebase:firebase-perf:17.0.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.7'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation project(':test-utils')
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.1'
    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestImplementation 'androidx.arch.core:core-testing:2.0.1'
    androidTestImplementation 'org.mockito:mockito-core:2.19.0', { exclude group: 'net.bytebuddy' }
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.19.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    debugImplementation 'androidx.fragment:fragment-testing:1.1.0-alpha09'
}

static def isMinifyEnabledForBuild() {
    String minifyStatus = System.getenv("MINIFY_ENABLED")
    if (minifyStatus != null) {
        return minifyStatus.toBoolean()
    }
    return false
}

project.tasks.whenTaskAdded {
    if (it.name == "testReleaseUnitTest") {
        it.enabled = false
    }
}

apply plugin: 'com.google.gms.google-services'
