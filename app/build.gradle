apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'kotlin-allopen'

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'cat.xlagunas.data.OpenClass'
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "cat.xlagunas.viv"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode rootProject.gitCommitCount
        versionName rootProject.gitTag
        testInstrumentationRunner "cat.xlagunas.viv.commons.di.MockTestRunner"
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('../buildsystem/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('../buildsystem/production.keystore')
            storePassword System.getenv("PROD_KEYSTORE_PASSWORD")
            keyAlias System.getenv("PROD_KEY_ALIAS")
            keyPassword System.getenv("PROD_KEY_PASSWORD")
        }
    }

    flavorDimensions "version"

    productFlavors {
        production {
            buildConfigField "String", "ENDPOINT", '"https://andrtc.xlagunas.cat"'
        }

        development {
            versionNameSuffix '.local'
            buildConfigField "String", "ENDPOINT", '"http://192.168.0.156:8080"'
//            buildConfigField "String", "ENDPOINT", '"http://10.239.162.28:8080"'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = ["appName": '@string/app_name']
        }

        debug {
            minifyEnabled isMinifyEnabledForBuild()
            useProguard isMinifyEnabledForBuild()
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.dev'
            versionNameSuffix '.dev'
            manifestPlaceholders = ["appName": '@string/app_name_dev']
        }

    }

    dataBinding {
        enabled = true
    }

    testOptions {
        animationsDisabled = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
}



dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':data')
    implementation project(':domain')

    kapt 'com.google.dagger:dagger-compiler:2.16'
    kapt 'com.google.dagger:dagger-android-processor:2.16'
    implementation 'com.google.dagger:dagger:2.16'
    implementation 'com.google.dagger:dagger-android-support:2.16'

    kapt 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.jakewharton:butterknife:8.8.1'

    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    implementation 'com.android.support:design:28.0.0-alpha3'
    implementation 'com.android.support:support-media-compat:28.0.0-alpha3'
    implementation 'com.android.support:support-v4:28.0.0-alpha3'
    implementation 'com.android.support:appcompat-v7:28.0.0-alpha3'

    implementation 'android.arch.lifecycle:runtime:1.1.1'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.lifecycle:reactivestreams:1.1.1'
    implementation 'android.arch.navigation:navigation-fragment-ktx:1.0.0-alpha02'
    implementation 'android.arch.navigation:navigation-ui-ktx:1.0.0-alpha02'
    implementation 'android.arch.persistence.room:runtime:1.1.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.github.bumptech.glide:glide:4.7.1'
    kapt 'com.github.bumptech.glide:compiler:4.7.1'


    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.google.firebase:firebase-analytics:16.0.1'
    implementation 'com.google.firebase:firebase-perf:16.0.0'
    implementation 'com.google.firebase:firebase-messaging:17.0.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.16'

    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.jakewharton.timber:timber:4.7.0'

    testImplementation 'junit:junit:4.12'

    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.16'
    kaptTest 'com.google.dagger:dagger-compiler:2.16'

    androidTestImplementation("com.github.tomakehurst:wiremock:2.8.0") {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'asm', module: 'asm'
        exclude group: 'org.json', module: 'json'
    }

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestImplementation 'android.arch.navigation:navigation-testing-ktx:1.0.0-alpha02' // use -ktx for Kotlin
    androidTestImplementation 'android.arch.core:core-testing:1.1.1'
    androidTestImplementation 'org.mockito:mockito-core:2.19.0', { exclude group: 'net.bytebuddy' }
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.19.0'

}

static def isMinifyEnabledForBuild() {
    String minifyStatus = System.getenv("MINIFY_ENABLED")
    if (minifyStatus != null) {
        return minifyStatus.toBoolean()
    }
    return false
}

project.tasks.whenTaskAdded {
    if (it.name == "testReleaseUnitTest") {
        it.enabled = false
    }
}

apply plugin: 'com.google.gms.google-services'