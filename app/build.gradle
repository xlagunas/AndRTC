apply plugin: 'com.android.application'

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    defaultConfig {
        applicationId "cat.xlagunas.andrtc"
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
        versionCode 7
        versionName "1.1.7"
        vectorDrawables.useSupportLibrary = true

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file('../buildsystem/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        prod {
            manifestPlaceholders appName: "@string/app_name_prod"
        }
        dev {
            manifestPlaceholders appName: "@string/app_name_dev"
            applicationIdSuffix ".dev"
        }
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet false
        // if true, stop the gradle build if errors are found
        abortOnError true
        // if true, only report errors
        ignoreWarnings false
    }
}

configurations {
    prodDebugCompile
    devDebugCompile
    prodReleaseCompile
    devReleaseCompile
}

dependencies {
    def presentationDependencies = rootProject.ext.presentationDependencies
    def presentationTestDependencies = rootProject.ext.presentationTestDependencies
    def developmentDependencies = rootProject.ext.developmentDependencies

    prodDebugCompile project(path: ':data', configuration: 'prodDebug')
    devDebugCompile project(path: ':data', configuration: 'devDebug')
    prodReleaseCompile project(path: ':data', configuration: 'prodRelease')
    devReleaseCompile project(path: ':data', configuration: 'devRelease')
    compile project(':domain')
    compile project(':data')

    annotationProcessor presentationDependencies.daggerCompiler
    annotationProcessor presentationDependencies.butterKnifeCompiler
    annotationProcessor presentationDependencies.autoFactory

    compile presentationDependencies.dagger
    compile presentationDependencies.butterKnife
    compile presentationDependencies.recyclerView
    compile presentationDependencies.rxJava
    compile presentationDependencies.rxAndroid
    compile presentationDependencies.materialSupport
    compile presentationDependencies.materialSupport
    compile presentationDependencies.cardViewSupport
    compile presentationDependencies.appCompatSupport
    compile presentationDependencies.recyclerSupport
    compile presentationDependencies.supportV4
    compile presentationDependencies.gcmServices
    compile presentationDependencies.gcmAuth
    compile presentationDependencies.circleImageView
    compile presentationDependencies.glide
    compile presentationDependencies.percentSupport
    compile presentationDependencies.timber
    provided presentationDependencies.autoFactory
    compile developmentDependencies.leakCanary

    provided presentationDependencies.javaxAnnotation

    androidTestCompile presentationTestDependencies.mockito
    androidTestCompile presentationTestDependencies.dexmaker
    androidTestCompile presentationTestDependencies.dexmakerMockito
    androidTestCompile presentationTestDependencies.espresso
    androidTestCompile presentationTestDependencies.testingSupportLib
    androidTestCompile presentationTestDependencies.javawriter

}

apply plugin: 'com.google.gms.google-services'






